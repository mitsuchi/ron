op 30 : _ -> _
op 30 : _ --> _
op 30 : _ : _                # typing relation
op 50 : if _ then _ else _

syntax {
  t ::=                      # terms:
        true                 # constant true
      | false                # constant false
      | if t then t else t   # conditional

  v ::=                      # values:
        true                 # true value
      | false                # false value

  T ::=
        Bool
}

true : Bool
false : Bool

if t1 then t2 else t3 : T {
  t1 : Bool
  t2 : T
  t3 : T
}

if true then t2 else t3 -> t2   # E-IfTrue
if false then t2 else t3 -> t3  # E-ifFalse
# E-If
if t1 then t2 else t3 -> if t1' then t2 else t3 {
  t1 -> t1'
}

t1 --> t2 {
  t1 -> t3
  t3 --> t2
}
t --> t' {
  t -> t'
}
t --> t

main {
   if (if true then false else true) then false else true : T
}