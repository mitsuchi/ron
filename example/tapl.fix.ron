# simply typed lambda calculus : simple extension : full
use % for newline

op 99 : _ _
op 99 : succ _                   # successor
op 99 : pred _                   # predecessor
op 99 : iszero _                 # zero test
op 99 : fix _                    # fix
op 90 : not _
op 90 : X _
op 75 : _ * _                    # product type
op 70 : _ + _                    # sum type
op 70 : ( _ , _ )                # pair
op 60 : _ . _                    # projection
op 55 : _ as _                   # ascription
op 50 : let _ = _ in _
op 50 : λ _ : _ . _
op 50 : if _ then _ else _
op 50 : left _
op 50 : right _
op 50 : case _ of left _ => _ / right _ => _
op 50 : [ _ -> _ ] _
op 40 r : _ -> _
op 30 : _ : _
op 30 : _ \= _
op 25 : _ ; _
op 20 : _ , _
op 15 : _ : _ ∈ _
op 10 : _ ⊢ _ : _
op 10 : _ => _
op 10 : _ --> _
op 10 : free _ in _

syntax {
    t ::=                     # terms
          true                # constant true
        | false               # constant false
        | x                   # variables
        | λ x : T . t         # abstraction value
        | t t                 # application
        | if t then t else t  # conditional
        | 0                   # constant zero
        | succ t              # successor
        | pred t              # predecessor
        | iszero t            # zero test
        | t as T              # ascription
        | unit
        | let x = t in t      # let binding
        | ( t , t )           # pair
        | t . 1               # 1st projection
        | t . 2               # 2nd projection
        | left t              # sum : tagging left
        | right t             # sum : tagginf right
        | case t of left x => t / right x => t
        | fix t

    v ::=                     # values
          true
        | false
        |  λ x : T . t        # abstraction value
        | nv                  # numeric value
        | unit
        | ( v , v )           # pair value
        | left v              # sum value : tagging left
        | right v             # sum value : tagging right

    nv ::=                    # numeric values:
          0                   # zero
        | succ nv             # successor value
    
    # variables
    x ::= a | b | c | d | e | f | g | h | i | j | k | l | m | n

    T ::=                # types
          Bool           # type of booleans
        | Nat            # type of natural numbers
        | T -> T         # type of functions
        | Unit           # unit type
        | T * T          # product type
        | T + T          # sum type

    Γ ::=                # contexts
          φ              # empty context
        | Γ, x : T       # term variable binding

    s ::= t           # term
    y ::= x           # variable
    z ::= x           # variable

    # x, y, z : 変数を表すメタ変数：実際の変数は a から n までの1文字
    # t, s    : 項を表すメタ変数
}

# 評価文脈の文法
context syntax {
    E ::=                   
          _ t                  # 適用 s t の形はまず左側の s を簡約する
        | v _                  # 左側が値 v になったら右側を簡約する
        | succ _               # E-Succ
        | pred _               # E-Pred
        | iszero _             # E-IsZero
        | if _ then t else t   # E-If
        | _ as T               # E-Ascribe2
        | let x = _ in t       # E-Let
        | _ . 1                # E-Proj1
        | _ . 2                # E-Proj2
        | ( _ , t )            # E-Pair1 : ペアは左側から評価する
        | ( v , _ )            # E-Pair2 : 左側が値になったら右側へ
        | case _ of left x => t / right x => t
        | left _
        | right _
        | fix _
}

# 評価文脈の展開ルール
context rule {
    E [t] -> E [t'] {
        t -> t'
    }
}

# T-Abs
Γ ⊢ (λ x : T1 . t) : T1 -> T2 {
    Γ, x : T1 ⊢ t : T2
}

# T-App
Γ ⊢ t1 t2 : T2 {
    Γ ⊢ t1 : T1 -> T2
    Γ ⊢ t2 : T1
}

# T-Var
Γ ⊢ x : T {
    x : T ∈ Γ
}

# type for x in Γ is T <=> x : T ∈ Γ
x : T ∈ (Γ, x : T)
x : T1 ∈ (Γ, y : T2) {
    <x \= y>
    x : T1 ∈ Γ
}

# 右側が値になったらベータ簡約する
(λ x : T . t) v -> s' {
    [x -> v] t => s'
}

# 簡約の反射推移閉包
t1 --> t2 {
    t1 -> t3
    t3 --> t2
}
t1 --> t2 {
    t1 -> t2
}
t1 --> t1

# ベータ簡約
# [x -> s] t => u で、t の自由な x を s に置き換えると u になる、の意味

# x は x の中で自由なので s に置き換える
[x -> s] x => s
# x と異なる変数 y の中に x はないので y のまま
[x -> s] y => y {
    <x \= y>
}

# t1 t2 は、それぞれの項の中で置き換える
[x -> s] (t1 t2) => t1' t2' {
     [x -> s] t1 => t1'
     [x -> s] t2 => t2'
}

# true と false はそのまま
[x -> s] true => true
[x -> s] false => false

# 0 はそのまま
[x -> s] 0 => 0

# succ t は、t の中で置き換える
[x -> s] (succ t1) => succ t1' {
     [x -> s] t1 => t1'
}

# pred t は、t の中で置き換える
[x -> s] (pred t1) => pred t1' {
     [x -> s] t1 => t1'
}

# iszero t は、t の中で置き換える
[x -> s] (iszero t1) => iszero t1' {
     [x -> s] t1 => t1'
}

# fix t は、t の中で置き換える
[x -> s] (fix t1) => fix t1' {
     [x -> s] t1 => t1'
}

# if t1 then t2 else t3 は、各項の中で置き換える
[x -> s] (if t1 then t2 else t3) => if t1' then t2' else t3' {
     [x -> s] t1 => t1'
     [x -> s] t2 => t2'
     [x -> s] t3 => t3'
}
# λx. t1 の中に自由な x はないのでそのまま
[x -> s] (λx : T. t1) => λx : T. t1

# λy. t1 は、t1 の x を s に置き換えたものとしたいが
# s の中に自由な y があると λy. で束縛されて意味か変わってしまうので、
# そんな y がないという条件でそうする
[x -> s] (λy : T. t1) => λy : T. t2 {
    <x \= y>
    <not (free y in s)>
    [x -> s] t1 => t2
}

# λy. t1 で s の中に自由な y があるときは、
# そもそも λy. t1 の中の y を無関係な変数 z に変える
# その際は仮引数の y と t1 の中の自由な y を z に変える
[x -> s] (λy : T. t1) => λz : T. t2 {
    <x \= y>
    free y in s
    X z                  # 新しい変数 z を、以下を満たすように
    <x \= z>             # バックトラックを繰り返して探す
    <y \= z>             # つまり z は x, y や s, t1 の中の自由変数の
    <not (free z in s)>  # どれとも異なる
    <not (free z in t1)> # z の条件ここまで
    [y -> z] t1 => t3
    [x -> s] t3 => t2
}

# free x in t : 項 t に x が自由変数として出現する
free x in x
free x in t1 t2 {
    free x in t1
}
free x in t1 t2 {
    free x in t2
}
free x in λy : T. t {
    <x \= y>
    free x in t
}

# Nat
Γ ⊢ 0 : Nat
Γ ⊢ succ t1 : Nat {
  Γ ⊢ t1 : Nat
}
Γ ⊢ pred t1 : Nat {
  Γ ⊢ t1 : Nat
}
Γ ⊢ iszero t1 : Bool {
  Γ ⊢ t1 : Nat
}

pred 0 -> 0                      # E-PredZero
pred (succ nv) -> nv             # E-PredSucc
iszero 0 -> true                 # E-IsZeroZero
iszero (succ nv) -> false        # E-IsZeroSucc

# Bool

# T-True, T-False
Γ ⊢ true : Bool
Γ ⊢ false : Bool

# T-If
Γ ⊢ if t1 then t2 else t3 : T {
    Γ ⊢ t1 : Bool
    Γ ⊢ t2 : T
    Γ ⊢ t3 : T
}

if true then t2 else t3 -> t2   # E-IfTrue
if false then t2 else t3 -> t3  # E-ifFalse

# E-Ascribe
v as T -> v

# T-Ascribe
Γ ⊢ t as T : T {
    Γ ⊢ t : T
}

# unit -- derived form
# t1 ; t2 は (λ x : Unit. t2) t1 の糖衣構文
# ただし x は t2 の中に自由変数として現れない
(t1 ; t2) --> t3 {
    <not (free x in t2)>
    (λ x : Unit. t2) t1 --> t3
}

# T-Unit
Γ ⊢ unit : Unit

# unit は変数を含まないのでそのまま
[x -> s] unit => unit

# E-LetV
let x = v in t -> s {
    [x -> v] t => s
}

# T-Let
Γ ⊢ let x = t1 in t2 : T2 {
    Γ ⊢ t1 : T1
    Γ, x : T1 ⊢ t2 : T2
}

# Pair : (t1, t2) は、それぞれの項の中で置き換える
[x -> s] (t1, t2) => (t1', t2') {
     [x -> s] t1 => t1'
     [x -> s] t2 => t2'
}

# t . 1 は t の中で置き換える
[x -> s] t . 1 => t' . 1 {
     [x -> s] t => t'
}
[x -> s] t . 2 => t' . 2 {
     [x -> s] t => t'
}

# E-PairBeta
(v1, v2) . 1 -> v1
(v1, v2) . 2 -> v2

# T-Pair
Γ ⊢ (t1, t2) : T1 * T2 {
    Γ ⊢ t1 : T1
    Γ ⊢ t2 : T2
}
# T-Proj
Γ ⊢ t . 1 : T1 {
    Γ ⊢ (t1, t2) : T1 * T2
}
Γ ⊢ t . 2 : T2 {
    Γ ⊢ (t1, t2) : T1 * T2
}

# E-CaseLeft
case (left v) of left x1 => t1 / right x2 => t2 -> t3 {
    [x1 -> v] t1 => t3
}
case (right v) of left x1 => t1 / right x2 => t2 -> t3 {
    [x2 -> v] t1 => t3
}

# T-Left
Γ ⊢ left t1 : T1 + T2 {
    Γ ⊢ t1 : T1
}
# T-Right
Γ ⊢ right t2 : T1 + T2 {
    Γ ⊢ t2 : T2
}
# T-Case
Γ ⊢ case t0 of left x1 => t1 / right x2 => t2 : T {
    Γ ⊢ t0 : T1 + T2
    Γ, x1 : T1 ⊢ t1 : T
    Γ, x2 : T2 ⊢ t2 : T
}

# T-Fix
Γ ⊢ fix t1 : T {
    Γ ⊢ t1 : T -> T
}

# E-Fix
fix (λx : T. t) -> t' {
    [x -> fix (λx : T. t)] t => t'
}

main {
    fix (λe : (Nat -> Bool) . λn : Nat . if iszero n then true else (if iszero (pred n) then false else e (pred (pred n)))) (succ succ 0) --> w
}